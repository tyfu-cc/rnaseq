//
// Sort and index BAM file
//

include { SAMTOOLS_SORT      } from "../../../modules/zhanglab/samtools/sort"
include { SAMTOOLS_INDEX     } from "../../../modules/zhanglab/samtools/index"

workflow BAM_SORT_INDEX_SAMTOOLS {

    take:
    ch_bam   // channel: [ val(meta), [ bam ] ]
    ch_fasta // channel: [ val(meta), path(fasta) ]


    main:
    ch_versions = Channel.empty()

    SAMTOOLS_SORT( ch_bam, ch_fasta )
    ch_versions = ch_versions.mix(SAMTOOLS_SORT.out.versions.first())

    SAMTOOLS_INDEX( SAMTOOLS_SORT.out.bam )
    ch_versions = ch_versions.mix(SAMTOOLS_INDEX.out.versions.first())

    SAMTOOLS_SORT.out.bam
        .join(SAMTOOLS_INDEX.out.bai, by: [0], remainder: true)
        .join(SAMTOOLS_INDEX.out.csi, by: [0], remainder: true)
        .map {
            meta, bam, bai, csi ->
                if (bai) {
                    [ meta, bam, bai ]
                } else {
                    [ meta, bam, csi ]
                }
        }
        .set { ch_bam_bai }


    emit:
    bam      = SAMTOOLS_SORT.out.bam     // channel: [ val(meta), [ bam ] ]
    bai      = SAMTOOLS_INDEX.out.bai    // channel: [ val(meta), [ bai ] ]
    csi      = SAMTOOLS_INDEX.out.csi    // channel: [ val(meta), [ csi ] ]

    versions = ch_versions               // channel: [ versions.yml ]

}
